import org.labkey.gradle.util.GroupNames

task('metadataStudyArchive', type: Zip, group: "Data", description: "Assemble study archive with dataset metadata") {
    archiveFileName.set('snprc_r24_metadata.zip')
    destinationDirectory = project.file("${project.labkey.explodedModuleDir}/archives")
    from("resources/referenceStudy/") {
        include "study.xml"
        include "datasets/*"
        exclude "datasets/*.tsv"
    }
}
project.tasks.module.dependsOn(project.tasks.metadataStudyArchive)

task('datasetStudyArchive', type: Zip, group: "Data", description: "Assemble study archive with dataset data") {
    archiveFileName.set( 'snprc_r24.zip')
    destinationDirectory = project.file("${project.labkey.explodedModuleDir}/archives")
    from("resources/referenceStudy/") {
        include "study.xml"
        include "datasets/*"
    }
}

project.tasks.module.dependsOn(project.tasks.datasetStudyArchive)

sourceSets {
    transform {
        java {
            srcDirs = ['resources/transform/src']
        }
    }
}

dependencies {
    transformImplementation "org.apache.poi:poi:${poiVersion}"
    transformImplementation "org.apache.poi:poi-ooxml:${poiVersion}"
    transformImplementation "org.apache.poi:poi-ooxml-schemas:${poiVersion}"
    transformImplementation "org.apache.pdfbox:pdfbox:${pdfboxVersion}"
    transformImplementation "com.google.guava:guava:${guavaVersion}"
    transformImplementation "org.apache.commons:commons-collections4:${commonsCollections4Version}"
}

configurations {
    transformJars {
        canBeResolved = true
        extendsFrom(transformImplementation)
    }
}

project.task("transformJar",
        group: GroupNames.BUILD,
        type: Jar,
        description: "Create the transform jar file",
        {Jar jar ->
            jar.from project.sourceSets.transform.output
            jar.from { configurations.transformJars.collect { it.isDirectory() ? it : zipTree(it) }}
            jar.archiveBaseName.set("MicrobiomeTransform")
            jar.archiveClassifier.set("transform")
            jar.duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
            jar.destinationDirectory = new File(project.buildDir, "artifacts/snprc")
            jar.manifest {
                attributes "Main-Class": "MicrobiomeTransform"
            }
        }
)

project.artifacts {
    archives project.tasks.transformJar
}

project.tasks.module.dependsOn(project.tasks.transformJar)

