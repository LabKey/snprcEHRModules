import org.labkey.gradle.util.GroupNames
import org.labkey.gradle.util.BuildUtils

sourceSets {
    transform {
        java {
            srcDirs = ['resources/transform/src']
        }
    }
}

dependencies {
    transformImplementation "org.apache.poi:poi:${poiVersion}"
    transformImplementation "org.apache.poi:poi-ooxml:${poiVersion}"
    transformImplementation "org.apache.pdfbox:pdfbox:${pdfboxVersion}"
    transformImplementation "com.google.guava:guava:${guavaVersion}"
    transformImplementation "org.apache.commons:commons-collections4:${commonsCollections4Version}"
}

configurations {
    transformJars {
        canBeResolved = true
        extendsFrom(transformImplementation)
    }
}

project.tasks.register("transformJar", Jar)
        {Jar jar ->
            jar.group = GroupNames.BUILD
            jar.description = "Create the transform jar file"

            jar.from project.sourceSets.transform.output
            jar.from { configurations.transformJars.collect { it.isDirectory() ? it : zipTree(it) }}
            jar.archiveBaseName.set("MicrobiomeTransform")
            jar.archiveClassifier.set("transform")
            jar.duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
            jar.destinationDirectory.set(BuildUtils.getBuildDirFile(project, "artifacts/snprc"))
            jar.manifest {
                attributes "Main-Class": "MicrobiomeTransform"
            }
        }

project.artifacts {
    archives project.tasks.transformJar
}

project.tasks.named('module').configure { dependsOn(project.tasks.transformJar) }
